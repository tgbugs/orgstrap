# -*- org-adapt-indentation: nil; org-edit-src-content-indentation: 0; orgstrap-cypher: sha256; orgstrap-block-checksum: 9723322fe9626da20c5d20ff6909561e8c1c88dd457870b398608495c6615f25; orgstrap-norm-func-name: orgstrap-norm-func--prp-1\.0; -*-
# [[orgstrap][jump to orgstrap block for this file]]
#+title: Bootstrap an Org file using file local variables
#+options: num:nil \n:nil
#+LATEX_HEADER: \usepackage[margin=0.8in]{geometry}
#+LATEX_HEADER: \setlength\parindent{0pt}

# [[file:./README.pdf]]
# [[file:./README.html]]
#+HTML: <a href="https://melpa.org/#/orgstrap"><img src="https://melpa.org/packages/orgstrap-badge.svg"></a> <a href="https://stable.melpa.org/#/orgstrap"><img src="https://stable.melpa.org/packages/orgstrap-badge.svg"></a>
#+HTML: <img src="images/orgstrap-horse.svg" align="right" title="Why a horse? Read the source!">
# orgstrap
# Not quite a unicorn.
# If you wan't to grow up to be a unicorn you're going to have to
# pull yourself up by your own bootstraps!

=orgstrap= is a [[#specification][specification]] and tooling for bootstrapping Org files.

It allows Org files to describe their own requirements, and
define their own functionality, making them self-contained,
standalone computational artifacts, dependent only on Emacs
or other implementations of the Org-babel protocol in the future.

This file bootstraps itself to provide the functionality needed to use
=orgstrap= in any Org file.

=orgstrap= works with all versions of Emacs since =24.4= and all versions of Org since =8.2.10=.

Please see the [[#change-log][change log]] for the latest updates.
* Getting started
Using =orgstrap= is easy.

If you already have =orgstrap= installed you can enable it for any
Org file by running =M-x= =orgstrap-init= which will add the basic
=orgstrap= machinery to the current buffer.

If you don't have =orgstrap=, you can get it using itself.
1. Obtain the org mode source for this file. (e.g.
   [[https://raw.githubusercontent.com/tgbugs/orgstrap/master/orgstrap.org][from GitHub]]).
2. Open the source in Emacs[[#bootstrapping-to-emacs-bootstrapping-to-org][*]].
   (e.g. =M-x= =url-handler-mode= then =C-x C-f=
   # @@latex: \\@@
   =https://raw.githubusercontent.com/tgbugs/orgstrap/master/README.org=).
3. Decline the file local variables.
4. Inspect the [[#details][orgstrap block]] and file local variables.
5. Reload the file and accept the file local variables.
6. Congratulations you can now use =orgstrap= with your own files!

For a somewhat more sane and persistent installation experience
=orgstrap= is also available on [[https://melpa.org/#/orgstrap][melpa]].

A minor mode for editing orgstrapped files is included as =orgstrap-edit-mode=.
It is activated by =orgstrap-init=. When enabled it automatically updates
the =orgstrap-block-checksum= prop line local variable whenever the
=orgstrap= block changes.

If you do not use =orgstrap-edit-mode= then the easiest way to add the
orgstrap checksum to a file is to invoke =M-x= =orgstrap-add-block-checksum=.

=orgstrap= also includes =orgstrap-mode=, which is a regional minor mode
for =org-mode=. When enabled, =orgstrap-mode= detects, checks, and runs
orgstrap blocks when visiting Org files, superseding any embedded =eval:=
local variables.

The rest of this file is an overview of the use cases for =orgstrap= and
the implementation of =orgstrap= along with discussion and commentary.

If you are looking for examples of how to use =orgstrap= this files is a good place to start.
* Hello =orgstrap=
The bare minimum needed to make an =org-mode= file executable (with a bit of safety).
#+caption: [[file:./orgstrap-minimal.org]]
#+begin_src org :tangle ./orgstrap-minimal.org
# -*- orgstrap-cypher: sha256; orgstrap-block-checksum: 66ba9b040e22cc1d30b6f1d428b2641758ce1e5f6ff9ac8afd32ce7d2f4a1bae; orgstrap-norm-func-name: orgstrap-norm-func--prp-1\.0; -*-
# [[orgstrap][jump to orgstrap block for this file]]

,#+name: orgstrap
,#+begin_src elisp :results none :noweb yes
(message "orgstrap successful!") ; (ref:im-a-coderef-and-thats-ok)
,#+end_src

=orgstrap= a plain-text executable format. Powered by Org mode and Emacs.

# Local Variables:
# eval: (progn (setq-local orgstrap-min-org-version "8.2.10") (let ((actual (org-version)) (need orgstrap-min-org-version)) (or (fboundp #'orgstrap--confirm-eval) (not need) (string< need actual) (string= need actual) (error "Your org is too old! %s < %s" actual need))) (defun orgstrap-norm-func--prp-1\.0 (body) (let ((print-quoted nil)) (prin1-to-string (read (concat "(progn\n" body "\n)"))))) (unless (boundp 'orgstrap-norm-func) (defvar orgstrap-norm-func orgstrap-norm-func-name)) (defun orgstrap-norm-embd (body) (funcall orgstrap-norm-func body)) (unless (fboundp #'orgstrap-norm) (defalias 'orgstrap-norm 'orgstrap-norm-embd)) (defun orgstrap--confirm-eval-minimal (lang body) (not (and (member lang '("elisp" "emacs-lisp")) (eq orgstrap-block-checksum (intern (secure-hash orgstrap-cypher (orgstrap-norm body))))))) (unless (fboundp 'orgstrap--confirm-eval) (defalias 'orgstrap--confirm-eval 'orgstrap--confirm-eval-minimal)) (let ((ocbe org-confirm-babel-evaluate)) (setq-local orgstrap-norm-func orgstrap-norm-func-name) (setq-local org-confirm-babel-evaluate #'orgstrap--confirm-eval) (unwind-protect (save-excursion (org-babel-goto-named-src-block "orgstrap") (org-babel-execute-src-block)) (setq-local org-confirm-babel-evaluate ocbe))))
# End:
#+end_src
* Inspiration
By default =org-mode= source block headers only take existing elisp functions as arguments.

This means that header arguments can become extremely verbose.

Wouldn't it be great if you could use the magical mystical power of =defun=
inside an org file itself to provide simple, reusable functionality rather
than +copying and pasting+ +yanking and putting+ killing and yanking raw
elisp around the buffer?

With =orgstrap= you can.

=orgstrap= makes sure that the functionality that you need is available when you need it.
Whether it is =(defun dir-tramp-sudo (host) (format "/ssh:%s|sudo:%s:" host host))= to
simplify a pattern for remote execution when using the =:dir= header argument, or a
function to detect and set the right environment variables, =orgstrap= is there for you.
* Use cases
=orgstrap= specifies what is essentially a plain-text executable file format.
Thus, it can be used for nearly everything[fn::Now, whether it *should* be....].

While many (including the author) might find this to be totally radically awesome,
there are much better, saner, and safer ways to execute arbitrary code than to hash
some elisp blocks and use Emacs file local variables to automatically eval a specially
named source block only when it matches the hash.

#+caption: Things you can do with arbitrary code execution and checksums.
#+name: table-use-cases
|----------------------------------------+------------------+--------------------------------|
| Use case                               | Good idea        | Alternative                    |
|----------------------------------------+------------------+--------------------------------|
| Always run defuns used in file         | ✅ Yes           | init.el, =C-c C-c=             |
| Install elisp code directly            | ❌ No            | Use =packages.el=              |
| Self tangling files                    | ✅ I do it       | =C-c C-v C-t=                  |
| Install packages required by file      | Probably         | System package manager         |
| Create an Emacs based botnet           | ✅ ✅ Definitely | ???                            |
| Replace hard to follow instructions    | ✅ Yes           | Hard to follow instructions    |
| Tangle git hook files for publishing   | ✅ Yes           | Manually tangle                |
| System specific behavior without edits | ✅ Yes           | #+name: literal blocks via =:= |
| Version control for source blocks      | ❌ ❌ Please no  | git, hg, svn, anything please  |
| Detect and set environment variables   | ✅ Yes           |                                |
|----------------------------------------+------------------+--------------------------------|
# Actually I'm kind of hyped for though of describing the system used to version
# control the code in the file itself. Not so simple to pull off though.
# It only sort of works in this case because we have the rest of the file under
# version control in another system.
* Details
:PROPERTIES:
:CUSTOM_ID: details
:END:
The first elisp source block named =orgstrap= in an org file is
automatically run using an =eval:= file local variable. Users can
review and add the file local variables to their known safe list
so that the code can be run in the future without the need to bother
them again.

When opening a file for the first time, users should decline the local
variables, review the =eval:= local variable and the =orgstrap= block
directly, and then reload, revisit, or =M-x= =org-mode= and only then
accept the local variables. This only needs to be done once for the
=eval:= local variables (unless they are updated).

This is the =orgstrap= block that is used for this file.

#+caption: The =orgstrap= block that is used for this file.
#+name: orgstrap
#+begin_src elisp :results none :noweb no-export
;; This is an example that also nowebs in the source for
;; `orgstrap-init' and `orgstrap-add-block-checksum' along
;; with the rest of the orgstrap machinery so it is easy to
;; use orgstrap to create and update orgstrap blocks

<<orgstrap-run-helper-defuns>>
<<orgstrap-edit-helper-defuns>>
<<orgstrap-init-helper-defuns>>
<<orgstrap-extra-helper-defuns>>

;; helper functions to update examples
(defun orgstrap--update-examples ()
  "Use with `orgstrap-on-change-hook' to automatically keep the contents
of the example blocks in sync."
  (let ((pairs `(("local-variables-prop-line-example" ,(orgstrap--local-variables-prop-line-string))
                 ("local-variables-example" ,(orgstrap--file-local-variables-string)))))
    (mapcar (lambda (name-content) (apply #'orgstrap-update-src-block name-content)) pairs)))

(defun orgstrap--local-variables-prop-line-string ()
  "Copy the first logical line of the file since it is easier and faster
than trying to sort out which variables were or were not in the prop line."
;; XXX NOTE There are some cases involving bootstrapping to emacs where the first line of
;;an org-mode file is a shebang, but we will deal with those if and when they arrise
  (buffer-substring-no-properties 1 (save-excursion (goto-char 0) (next-logical-line) (point))))

(defun orgstrap--file-local-variables-string ()
  (with-temp-buffer
    (org-mode)
    (orgstrap--add-file-local-variables orgstrap-use-minimal-local-variables)
    (buffer-string)))

;; tangle blocks and update examples on change
(add-hook 'orgstrap-on-change-hook #'org-babel-tangle nil t)
(add-hook 'orgstrap-on-change-hook #'orgstrap--update-examples nil t)
;; enable orgstrap mode locally for this file when this block runs
(orgstrap-edit-mode)

(message "orgstrap complete!")
#+end_src

The headers for the block above look like this.
#+name: orgstrap-example
#+begin_example org :eval never :noweb no
,#+name: orgstrap
,#+begin_src elisp :results none :noweb no-export
<<orgstrap>>
,#+end_src
#+end_example

Additional machinery is provided as part of this file to update the local
variable value of =orgstrap-block-checksum= so that only known blocks can
be run. Note that this DOES NOT PROTECT against someone changing the block
and the checksum at the same time and sending you a malicious file! You need
an alternate and trusted source against which to verify the checksum of the
=orgstrap= block.
** Portability
A couple of notes on portability and backward compatibility with older
versions of Emacs. I have tried to get =orgstrap= running on emacs-23,
however the differences between org =6.33x= and org =8.2.10= are too
large to be overcome without significant additional code. First, all
uses of =(setq-local var "value")= have to be changed to
=(set (make-local-variable 'var) "value")= so that the local variable
eval code can run. However once that is done, you discover that all of
the org-babel functions are missing, and then you will discover that
emacs-23 doesn't support lexical binding. Therefore, we don't support
emacs-23 and older versions.
* Specification
:PROPERTIES:
:CUSTOM_ID: specification
:END:
# Except for this comment, comments in the spec are not official parts of the spec.
The specification for orgstrap makes extensive use of terminology
derived from the Emacs manual section on
[[info:emacs#Specifying File Variables][Specifying File Variables]]
and the Org manual section on the
[[info:org#Structure of Code Blocks][Structure of Code Blocks]].

What the Emacs manual calls the first line or prop-line is referred
to in this document as the =prop line= and the variables specified in
it are referred to as =prop line local variables=. What the Emacs
manual explicitly calls the =local variables list= we refer to in the
same way[fn::In other sections of the readme that contains this
specification the nomenclature is inconsistent, and refers to these
variously as end local variables or simply as local variables or file
local variables.].

What the Org manual refers to as a =source code block= we refer to in the
same way.

In order for an Org mode file to support the use of =orgstrap= it must
contain the following.

The =prop line= of the Org file must include three local variables:
=orgstrap-cypher=, =orgstrap-norm-func-name=, and =orgstrap-block-checksum=.

Anywhere in the rest of the file there must be an Org =source code block=
that has the =<name>= =orgstrap= with whitespace preceding the =o= and only
whitespace following the =p= until a newline. Newline and whitespace are as
defined by [[https://orgmode.org/worg/dev/org-syntax.html][Org mode syntax]].
This =source code block= is henceforth referred to as the =orgstrap block=.
If there is more than one =source code block= with the =<name>= =orgstrap=
then the =source code block= that starts closest to the beginning of the file
is the =orgstrap block=.

The =<language>= for the =orgstrap block= must be =elisp= or =emacs-lisp=. [fn::
It is possible that other languages might be supported in the future. However,
that is somewhat challenging given that Org and Orb-babel only implicitly
specify that a conforming implementation that can execute =source code blocks=
must support Emacs lisp =source code blocks= and the use of Emacs lisp in
header arguments. There is an infinitesimal possibility that Org-babel will
support the use of other languages for inline header arguments since it
already supports them via blocks and it is not trivial to allow additional
languages to be used inline without some additional way to indicate the language
in use for a particular block. On the other hand, there is a small possibility
that other languages could be supported in the =orgstrap block= by specifying
them as part of the =local variables list=. However it is not clear that this
is needed, because it is possible to specify a small orgstrap block that can
ensure that the required Org-babel language implementations are installed and
then securely run those blocks. This block can probably be stripped down
sufficiently to make it possible to implement only the subset of elisp
required to run that block.]

Everything else about the =orgstrap block= is delegated to Org mode, including
header arguments, and noweb expansion.

# TODO With the possible exception being that a header of the form
# :var orgstrap-enable-optional=(identity nil) might be added to
# make it possible for the user to toggle optional dependencies
# obviously authors can do whatever they want with the block and
# set as many :vars to t or nil as they want to give users as much
# or as little control over what is run as they desire, this should
# probably just go in as an example, with note that this is one of the
# reasons why we don't hash :vars but also why users need to check those

# I'm 99% certain that embedding orgstrap-norm-func in the local variables list
# should NOT be required as part of the specification. I do that in the current
# implementation, but the 3000 char limit for the local variables list is going
# to pose quite the challenge for the portable implementation, and thus I think
# all the spec needs to say is that an implementation must be able to reproduce
# the orgstrap block hash when the whole file hash is the same.

When provided with the same file whose =orgstrap block= was originally hashed
(where "the same file" means a file with the same checksum when hashed using
the algorithm specified by the =orgstrap-cypher= variable), a conforming
implementation must be able to do the following.

A conforming implementation must be able to reproduce the =orgstrap-block-checksum=
using only the information contained in the =orgstrap-cypher= and
=orgstrap-norm-func-name= =prop line local varaibles=, and information
contained in the rest of the file explicitly excluding the contents of
the =orgstrap-cypher= =prop line local varaible=. The most obvious
additional information required being the contents of the =orgstrap block= [fn::
The reference implementation provided in the readme containing this specification
uses an Emacs =eval:= local variable in the =local variables list=. This is a maximal
implementation that is only dependent on the core Emacs implementation. One could
imagine a minimal implementation which checks whether orgstrap is present on the
current system and calls =(orgstrap-current-buffer)= if so, and otherwise raises
an error asking the user to install orgstrap or upgrade their version of Org or
Emacs. In fact one could imagine a case where only the prop line local variables
are needed. There is a fine balance between portability and compactness since
a minimal implementation has to make more assumptions about the systems it will
run on. Multi-stage orgstrap, or other means of bootstrapping a working runtime
for an Org file such as the process implemented in the
[[#bootstrapping-to-emacs-bootstrapping-to-org][Bootstrapping to Emacs, bootstrapping to Org.]]
section of this readme are ongoing areas of exploration.].

#+begin_quote
One implementation detail is that conforming implementations
must implement noweb expansion and coderef removal prior to
passing the contents of the =orgstrap block= to a normalization
function.
#+end_quote

Normalization functions that produce different output given the same
input for at least one input must have different names. One way this
can be achieved is by suffixing a name with a version number.

In order for an orgstrap normalization function name to be considered
official it must have an implementation bearing that name in the
[[#normalization-functions][Normalization functions]] section of the
readme that contains this specification. Once a function has been
named, no other function shall ever bear the same name unless for
all inputs it produces output that is byte-identical to the output
of all other previous implementations of the function bearing that
name.

#+begin_quote
A key point about =orgstrap-norm-func-name= is that the implementation
of these functions must be agreed upon by various implementations, if a user
inserts a fake hash, implementations should deal with it by running the
normalization and hashing process again using a known-conforming implementation
on a system that they control.
#+end_quote
* Local Variables
** Overview
Local variables must go at the end of a file.

We have to use =setq-local= on =org-confirm-babel-evaluate= because it is marked
as a =safe-local-variable=.  In this context this workaround seems reasonable and
not malicious because the use of =eval:= should alert users that some arbitrary
stuff is going on and that they should be on high alert to check it.

Below in [[#definitions][Definitions]] there is a more readable
version of what the compacted local variables code at the end of the file is doing.
*Always check that the =eval:= local variables in unknown orgstrapped files*
*match a known set when reviewing and accepting local variables.*
# TODO it is entirely possible to automate that check
# but not without already having orgstrap available.
# TODO publish the hashes of the eval sexps.
** Org version support
Different versions of the =orgstrap= local variables work with
different versions of =org-mode=. We include an explicit version
check and fail so that strange partial successes can be avoided
and so that newer versions of the local variables can be simplified
when backward compatibility is not needed. For example one might
imagine a future where no local variables are needed in the file
at all, only the cypher and the checksum because we managed to
get support for the convention built into =org-mode= directly.

This will also allow us to streamline which block to use based
on whether noweb is being used. If it is not then we can decide
automatically.

If orgstrap is installed, we use the installed version of orgstrap
anyway so don't bother.
#+name: orgstrap-check-org-version
#+begin_src elisp
(let ((actual (org-version))
      (need orgstrap-min-org-version))
  (or (fboundp #'orgstrap--confirm-eval) ; orgstrap with portable is already present on the system
      (not need)
      (string< need actual)
      (string= need actual)
      (error "Your Org is too old! %s < %s" actual need)))
#+end_src
#+caption: Portability note.
#+begin_quote
=string<= must be used in order to support emacs-24
#+end_quote
** Normalization
*** Shared normalization machinery
Shared normalization code embedded as eval local variables.
#+caption: Shared normalization code embedded as eval local variables.
#+name: orgstrap-normalization-common-embed
#+begin_src elisp
(unless (boundp 'orgstrap-norm-func)
  (defvar orgstrap-norm-func orgstrap-norm-func-name))

(defun orgstrap-norm-embd (body)
  "Normalize BODY."
  (funcall orgstrap-norm-func body))

(unless (fboundp #'orgstrap-norm)
  (defalias 'orgstrap-norm #'orgstrap-norm-embd))
#+end_src

Normalization functions for orgstrap.el.
#+caption: Normalization functions for orgstrap.el.
#+name: orgstrap-code-normalization-functions
#+begin_src elisp :eval never :noweb yes
(defun orgstrap-norm (body)
  "Normalize BODY."
  (if orgstrap--debug
      (orgstrap-norm-debug body)
    (funcall orgstrap-norm-func body)))

(defun orgstrap-norm-debug (body)
  "Insert BODY normalized with NORM-FUNC into a buffer for easier debug."
  (let* ((print-quoted nil)
         (bname (format "body-norm-%s" emacs-major-version))
         (buffer (let ((existing (get-buffer bname)))
                   (if existing existing
                     (create-file-buffer bname))))
         (body-normalized (funcall orgstrap-norm-func body)))
    (with-current-buffer buffer
      (erase-buffer)
      (insert body-normalized))
    body-normalized))

;; orgstrap normalization functions

<<block-orgstrap-norm-func--prp-1.0>>
#+end_src

#+caption: XXX portability note
#+begin_quote
For emacs < 26 (org < 9) either lowercase =#+caption:= must be placed _BEFORE_ =#+name:=
OR =#+CAPTION:= must be uppercase and can come after =#+name:=,
otherwise =#+name:= will not be associated with the block.
What a fun bug.

Addendum. Apparently in the older version of Org =:noweb= is always yes.
Therefore if you forget to set =:noweb= on a block, testing against emacs
24 or 25 will alert you to this.
#+end_quote
*** Normalization functions
:PROPERTIES:
:CUSTOM_ID: normalization-functions
:END:
**** prp-1.0
#+name: orgstrap-code-normalization--prin1-read-progn-1.0
#+begin_src elisp :eval never
(let ((print-quoted nil))
  (prin1-to-string (read (concat "(progn\n" body "\n)"))))
#+end_src

#+name: block-orgstrap-norm-func--prp-1.0
#+begin_src elisp :noweb yes :eval never
(defun orgstrap-norm-func--prp-1.0 (body)
  "Normalize BODY using prp-1.0."
  <<orgstrap-code-normalization--prin1-read-progn-1.0>>)
#+end_src
Normalize BODY by wrapping in =progn=, calling =read=, and then =prin1-to-string=.
There are still unresolved issues if tabs are present in the orgstrap block which
is why 1.0 is included. =print-quoted= is critical for consistent hashing.

=prin1-to-string= is used to normalize the code in the orgstrap block,
removing any comments and formatting irregularities. This is important
for two reasons.

First it helps prevent denial of service attacks against human auditors
who have low bandwidth for detecting fiddly changes.

Second, normalization that ignores comments makes it possible to improve
the documentation of code without changing the checksum. Hopefully this
will reduce one of the obstacles to enhancing the documentation of orgstrap
code and blocks over time since rehashing will not be required when the
meaningful code itself has not changed.

=(print-quoted nil)= is needed for backward compatibility due to a change
to the default from =nil= to =t= in emacs-27 (sigh). See
[[orgit-rev:~/git/NOFORK/emacs::72ee93d68daea00e2ee69417afd4e31b3145a9fa][emacs commit 72ee93d68daea00e2ee69417afd4e31b3145a9fa]].
** Definitions
:PROPERTIES:
:CUSTOM_ID: definitions
:END:
These blocks are nowebbed into ref:orgstrap-init-helper-defuns and are
used directly by =orgstrap-init= to populate file local variables.

The portable confirm eval is extracted to its own block so that we can
include it as a backstop for users who have orgstrap installed but are
running an older version of =org-mode= than is supported by the file
that they are trying to load.
#+caption: Portable confirm eval.
#+name: orgstrap-portable-confirm-eval
#+begin_src elisp :eval never :noweb yes
;;;###autoload
(defun orgstrap--confirm-eval-portable (lang body)
  ;; `org-confirm-babel-evaluate' will prompt the user when the value
  ;; that is returned is non-nil, therefore we negate positive matchs
  (not (and (member lang '("elisp" "emacs-lisp"))
            (let* ((body (orgstrap--expand-body (org-babel-get-src-block-info)))
                   (body-normalized (orgstrap-norm body))
                   (content-checksum
                    (intern
                     (secure-hash
                      orgstrap-cypher
                      body-normalized))))
              ;;(message "%s %s" orgstrap-block-checksum content-checksum)
              ;;(message "%s" body-normalized)
              (eq orgstrap-block-checksum content-checksum)))))
;; portable eval is used as the default implementation in orgstrap.el
;;;###autoload
(defalias 'orgstrap--confirm-eval #'orgstrap--confirm-eval-portable)
#+end_src

#+caption: Minimal confirm eval.
#+name: orgstrap-minimal-confirm-eval
#+begin_src elisp
(defun orgstrap--confirm-eval-minimal (lang body)
  (not (and (member lang '("elisp" "emacs-lisp"))
            (eq orgstrap-block-checksum
                (intern
                 (secure-hash
                  orgstrap-cypher
                  (orgstrap-norm body)))))))
(unless (fboundp 'orgstrap--confirm-eval)
  ;; if `orgstrap--confirm-eval' is bound use it since it is
  ;; is the portable version XXX NOTE the minimal version will
  ;; not be installed as local variables if it detects that there
  ;; are unescaped coderefs since those will cause portable and minimal
  ;; to produce different hashes
  (defalias 'orgstrap--confirm-eval #'orgstrap--confirm-eval-minimal))
#+end_src

Once =orgstrap--confirm-eval= is defined the rest of the =eval:= local variables are the same.

#+caption: common local variables
#+name: orgstrap-file-local-variables-common
#+begin_src elisp :eval never
(let ((ocbe org-confirm-babel-evaluate))
  (setq-local orgstrap-norm-func orgstrap-norm-func-name)
  (setq-local org-confirm-babel-evaluate #'orgstrap--confirm-eval)
  (unwind-protect
      (save-excursion
        (org-babel-goto-named-src-block ,orgstrap-orgstrap-block-name) ; quasiquoted when nowebbed
        (org-babel-execute-src-block))
    (setq-local org-confirm-babel-evaluate ocbe)))
#+end_src

Since =orgstrap-norm-func= is a dynamic variable it simplifies the
potential future case where we don't embed the normalization function,
still not sure if we really want to do that though
** Note on noweb support
The minimal set of local variables only works if you don't use noweb
or if you are using Org =>== =9.3.8=.

The portable set of local variables described below works with versions of
Org as far back as =8.2.10= (the version bundled with =emacs-24.5=).
** Note on coderefs
Older versions of =org-mode= do not know what to do with coderefs.
The simplest solution is to hide them in comments as =;(ref:coderef)=
if you need them. See [[(clrin)]] and [[(oab)]] for examples in this file.
** How local variables appear in the file
# DO NOT EDIT THESE BLOCKS THEY ARE UPDATED AUTOMATICALLY
Here is the prop line from the first line of this file that
includes the cypher and checksum of the =orgstrap= block.
#+name: local-variables-prop-line-example
#+begin_src org :eval never
# -*- org-adapt-indentation: nil; org-edit-src-content-indentation: 0; orgstrap-cypher: sha256; orgstrap-block-checksum: 9723322fe9626da20c5d20ff6909561e8c1c88dd457870b398608495c6615f25; orgstrap-norm-func-name: orgstrap-norm-func--prp-1\.0; -*-
#+end_src

# BE VERY CAREFUL WITH MANUAL EDITS
# If this block is being edited manually the automatic update will not work.
Here are the local variables from the end of the file.
#+name: local-variables-example
#+begin_src org :eval never

# Local Variables:
# eval: (progn (setq-local orgstrap-min-org-version "8.2.10") (let ((actual (org-version)) (need orgstrap-min-org-version)) (or (fboundp #'orgstrap--confirm-eval) (not need) (string< need actual) (string= need actual) (error "Your Org is too old! %s < %s" actual need))) (defun orgstrap-norm-func--prp-1\.0 (body) (let ((print-quoted nil)) (prin1-to-string (read (concat "(progn\n" body "\n)"))))) (unless (boundp 'orgstrap-norm-func) (defvar orgstrap-norm-func orgstrap-norm-func-name)) (defun orgstrap-norm-embd (body) (funcall orgstrap-norm-func body)) (unless (fboundp #'orgstrap-norm) (defalias 'orgstrap-norm #'orgstrap-norm-embd)) (defun orgstrap-org-src-coderef-regexp (_fmt &optional label) (let ((fmt org-coderef-label-format)) (format "\\([:blank:]*\\(%s\\)[:blank:]*\\)$" (replace-regexp-in-string "%s" (if label (regexp-quote label) "\\([-a-zA-Z0-9_][-a-zA-Z0-9_ ]*\\)") (regexp-quote fmt) nil t)))) (unless (fboundp #'org-src-coderef-regexp) (defalias 'org-src-coderef-regexp #'orgstrap-org-src-coderef-regexp)) (defun orgstrap--expand-body (info) (let ((coderef (nth 6 info)) (expand (if (org-babel-noweb-p (nth 2 info) :eval) (org-babel-expand-noweb-references info) (nth 1 info)))) (if (not coderef) expand (replace-regexp-in-string (org-src-coderef-regexp coderef) "" expand nil nil 1)))) (defun orgstrap--confirm-eval-portable (lang body) (not (and (member lang '("elisp" "emacs-lisp")) (let* ((body (orgstrap--expand-body (org-babel-get-src-block-info))) (body-normalized (orgstrap-norm body)) (content-checksum (intern (secure-hash orgstrap-cypher body-normalized)))) (eq orgstrap-block-checksum content-checksum))))) (defalias 'orgstrap--confirm-eval #'orgstrap--confirm-eval-portable) (let ((ocbe org-confirm-babel-evaluate)) (setq-local orgstrap-norm-func orgstrap-norm-func-name) (setq-local org-confirm-babel-evaluate #'orgstrap--confirm-eval) (unwind-protect (save-excursion (org-babel-goto-named-src-block "orgstrap") (org-babel-execute-src-block)) (setq-local org-confirm-babel-evaluate ocbe))))
# End:
#+end_src
* Code
** =orgstrap= implementation
This section contains the implementation of functions to calculate
=orgstrap-block-checksum= and set it as a prop line local variable.
It also contains functions to embed the bootstrapping code as an
=eval:= local variable in the local variables list, along with other
quality of life functionality for the user such as =orgstrap-mode=,
=orgstrap-edit-mode=, and =orgstrap-init=.
# [[info:elisp#File Local Variables][info:elisp#File Local Variables]] is a useful reference
*** Expand
Testing =org-src-coderef-regexp= with =fboundp= in ref:orgstrap-expand-body
is needed due to changes in the behavior of =org-babel-get-src-block-info=
roughly around the =9.0= release.

The changes in behavior for =org-babel-get-src-block-info= are commits
orgit-rev:~/git/NOFORK/org-mode::88659208793dca18b7672428175e9a712af7b5ad and
orgit-rev:~/git/NOFORK/org-mode::9738da473277712804e0d004899388ad71c6b791. They
both occur before the introduction of =org-src-coderef-regexp= in
orgit-rev:~/git/NOFORK/org-mode::9f47b37231b3c45afcd604a191e346200bd76e98.
All of this happend before orgit-rev:~/git/NOFORK/org-mode::release_9.0. By
testing =org-src-coderef-regexp= with =fboundp= there are only a tiny number
of versions where there might be some inconsistent behavior, e.g.
orgit-rev:~/git/NOFORK/org-mode::release_8.3.6, but I that the probability
that anyone anywhere is running one of those versions is approximately zero.

#+name: orgstrap-expand-body
#+begin_src elisp :eval never
(defun orgstrap-org-src-coderef-regexp (_fmt &optional label)
  "Backport `org-src-coderef-regexp' for 24 and 25.
See the upstream docstring for info on LABEL.
_FMT has the wrong meaning in 24 and 25."
  (let ((fmt org-coderef-label-format))
    (format "\\([:blank:]*\\(%s\\)[:blank:]*\\)$"
            (replace-regexp-in-string
             "%s"
             (if label
                 (regexp-quote label)
               "\\([-a-zA-Z0-9_][-a-zA-Z0-9_ ]*\\)")
             (regexp-quote fmt)
             nil t))))
(unless (fboundp #'org-src-coderef-regexp)
  (defalias 'org-src-coderef-regexp #'orgstrap-org-src-coderef-regexp))
(defun orgstrap--expand-body (info)
  "Expand noweb references in INFO body and remove any coderefs."
  ;; this is a backport of `org-babel--expand-body'
  (let ((coderef (nth 6 info))
        (expand
         (if (org-babel-noweb-p (nth 2 info) :eval)
             (org-babel-expand-noweb-references info)
           (nth 1 info))))
    (if (not coderef)
        expand
      (replace-regexp-in-string
       (org-src-coderef-regexp coderef) "" expand nil nil 1))))
#+end_src
*** Run
#+caption: run helpers
#+name: orgstrap-run-helper-defuns
#+begin_src elisp :noweb yes
(require 'cl-lib)

(defcustom orgstrap-always-edit nil
  "If non-nil then command `orgstrap-mode' will activate command `orgstrap-edit-mode'.")

;; orgstrap-mode implementation

(defun orgstrap--org-buffer ()
  "Only run when in `org-mode' and command `orgstrap-mode' is enabled.
Sets further hooks."
  (add-hook 'before-hack-local-variables-hook #'orgstrap--before-hack-lv nil t))

(defun orgstrap--before-hack-lv ()
  "Cull any orgstrap eval local variables.
If `orgstrap' is used in the current buffer, add hook to run the orgstrap block."
  (remove-hook 'before-hack-local-variables-hook #'orgstrap--before-hack-lv t)
  (add-hook 'hack-local-variables-hook #'orgstrap--hack-lv nil t)
  (orgstrap--cull-eval-local-variables))

(defun orgstrap--used-in-current-buffer-p ()
  "Return t if all the required orgstrap prop line local variables are present."
  (and (boundp 'orgstrap-cypher) orgstrap-cypher
       (boundp 'orgstrap-block-checksum) orgstrap-block-checksum
       (boundp 'orgstrap-norm-func-name) orgstrap-norm-func-name))

(defun orgstrap--hack-lv ()
  "If orgstrap is present, run the orgstrap block for the current buffer."
  (remove-hook 'hack-local-variables-hook #'orgstrap--hack-lv)
  (when (orgstrap--used-in-current-buffer-p)
    <<orgstrap-file-local-variables-common>>
    (when orgstrap-always-edit
      (orgstrap-edit-mode))))

(defun orgstrap--match-eval-local-variables (pair)
  "Return nil if PAIR matchs any eval local variable used by orgstrap.
Avoid false positives if possible if at all possible."
  (not (and (eq (car pair) 'eval)
            (message "%s" (cdr pair))
            ;; keep the detection simple for now, any eval lv that
            ;; so much as mentions orgstrap is nuked, and in the future
            ;; if orgstrap-nb is used we may need to nuke that too
            (string-match "orgstrap" (prin1-to-string (cdr pair))))))

(defun orgstrap--cull-eval-local-variables ()
  "When command `orgstrap-mode' is enabled cull orgstrap file local variables."
  (when (eq major-mode 'org-mode)
    (remove-hook 'before-hack-local-variables-hook #'orgstrap--cull-eval-local-variables)
    (let ((clva file-local-variables-alist))
      (setq-local file-local-variables-alist
                  (cl-remove-if-not #'orgstrap--match-eval-local-variables clva)))))

;;;###autoload
(define-minor-mode orgstrap-mode
  "A regional minor mode for `org-mode' that automatically runs orgstrap blocks.
When visiting an Org file or activating `org-mode', if orgstrap prop line local
variables are detect then use the installed orgstrap implementation to run the
orgstrap block. If orgstrap embedded local variables are present, they will not
be executed."
  nil "" nil
  (cond (orgstrap-mode
         (add-hook 'org-mode-hook #'orgstrap--org-buffer))
        (t
         (remove-hook 'org-mode-hook #'orgstrap--org-buffer))))
#+end_src
*** Edit
#+caption: edit helpers
#+name: orgstrap-edit-helper-defuns
#+begin_src emacs-lisp :results none :lexical yes :noweb yes
;;; edit helpers
(defvar orgstrap-orgstrap-block-name "orgstrap"
  "Set the default blockname to orgstrap by convention.
This makes it easier to search for orgstrap if someone encounters
an orgstrapped file and wants to know what is going on.")

(defvar orgstrap-default-cypher 'sha256
  "The default cypher passed to `secure-hash' when hashing blocks.")

(defvar orgstrap-norm-func #'orgstrap-norm-func--prp-1.0
  "Dynamic variable to simplify calling normalizaiton functions.
Defaults to `orgstrap-norm-func--prp-1.0'.")

(defvar orgstrap--debug nil
  "If non-nil run `orgstrap-norm' in debug mode.")

(defcustom orgstrap-on-change-hook nil
  "Hook run via `before-save-hook' when command `orgstrap-edit-mode' is enabled.
Only runs when the contents of the orgstrap block have changed."
  :type 'hook
  :group 'orgstrap)

(defcustom orgstrap-use-minimal-local-variables nil
  "Set whether minimal, smaller but less portable variables are used.
If nil then backward compatible local variables are used instead.
If the value is customized to be non-nil then compact local variables
are used and `orgstrap-min-org-version' is set accordingly.  If the
current version of org mode does not support the features required to
use the minimal variables then the portable variables are used instead."
  :type 'boolean
  :group 'orgstrap)

;; edit utility functions
(defun orgstrap--current-buffer-cypher ()
  "Return the cypher used for the current buffer.
The value is `orgstrap-cypher' if it is bound otherwise
`orgstrap-default-cypher' is returned."
  (if (boundp 'orgstrap-cypher) orgstrap-cypher orgstrap-default-cypher))

<<orgstrap-expand-body>>

<<orgstrap-code-normalization-functions>>

(defmacro orgstrap--with-block (blockname &rest macro-body)
  "Go to the source block named BLOCKNAME and execute MACRO-BODY.
The macro provides local bindings for four names:
`info', `params', `body-unexpanded', and `body'."
  (declare (indent defun))
  ;; consider accepting :lite or a keyword or something to pass
  ;; lite as an optional argument to `org-babel-get-src-block-info'
  ;; e.g. via (lite (equal (car macro-body) :lite)), given the
  ;; behavior when lite is not nil and the expected useage of this
  ;; macro I don't think we would ever want to pass a non nil lite
  `(save-excursion
     (let ((inhibit-message t)) ; inhibit-message only blocks from the message area not the log
       (org-babel-goto-named-src-block ,blockname))
     (unwind-protect
         (let* ((info (org-babel-get-src-block-info))
                (params (nth 2 info))
                (body-unexpanded (nth 1 info))
                ;; from `org-babel-check-confirm-evaluate'
                ;; and `org-babel-execute-src-block'
                (body (orgstrap--expand-body info)))
           ,@macro-body)
       (org-mark-ring-goto))))

(defun orgstrap--update-on-change ()
  "Run via the `before-save-hook' local variable.
Test if the checksum of the orgstrap block has changed,
if so update the `orgstrap-block-checksum' local variable
and then run `orgstrap-on-change-hook'."
  (let* ((elv (orgstrap--read-current-local-variables))
         (cpair (assoc 'orgstrap-block-checksum elv))
         (checksum-existing (if cpair (cdr cpair) nil))
         (checksum (orgstrap-get-block-checksum)))
    (unless (eq checksum-existing (intern checksum))
      (remove-hook 'before-save-hook #'orgstrap--update-on-change t)
      ;; for some reason tangling from a buffer counts as saving from that buffer
      ;; so have to remove the hook to avoid infinite loop
      (unwind-protect
          (save-excursion
            ;; using save-excusion here is a good for insurance against wierd hook issues
            ;; however it does not deal with the fact that updating `orgstrap-add-block-checksum'
            ;; adds an entry to the undo ring, which is bad
            ;;(undo-boundary)  ; undo-boundary doesn't quite work the way we want
            ;; related https://emacs.stackexchange.com/q/7558
            (orgstrap-add-block-checksum nil checksum)
            (run-hooks 'orgstrap-on-change-hook))
        (add-hook 'before-save-hook #'orgstrap--update-on-change nil t)))))

;; edit user facing functions
(defun orgstrap-get-block-checksum (&optional cypher)
  "Calculate the `orgstrap-block-checksum' for the current buffer using CYPHER."
  (interactive)
  (orgstrap--with-block orgstrap-orgstrap-block-name
    (ignore params body-unexpanded)
    (let ((cypher (or cypher (orgstrap--current-buffer-cypher)))
          (body-normalized (orgstrap-norm body)))
      (secure-hash cypher body-normalized))))

(defun orgstrap-add-block-checksum (&optional cypher checksum)
  "Add `orgstrap-block-checksum' to file local variables of `current-buffer'.
The optional CYPHER argument should almost never be used,
instead change the value of `orgstrap-default-cypher' or manually
change the file property line variable.  CHECKSUM can be passed
directly if it has been calculated before and only needs to be set."
  (interactive)
  (let* ((cypher (or cypher (orgstrap--current-buffer-cypher)))
         (orgstrap-block-checksum (or checksum (orgstrap-get-block-checksum cypher))))
    (when orgstrap-block-checksum
      (save-excursion
        (add-file-local-variable-prop-line 'orgstrap-cypher         cypher)
        (add-file-local-variable-prop-line 'orgstrap-norm-func-name orgstrap-norm-func)
        (add-file-local-variable-prop-line 'orgstrap-block-checksum (intern orgstrap-block-checksum))))
    orgstrap-block-checksum))

(defun orgstrap-run-block ()
  "Evaluate the orgstrap block for the current buffer."
  ;; bind to :orb or something like that
  (interactive)
  (save-excursion
    (org-babel-goto-named-src-block orgstrap-orgstrap-block-name)
    (org-babel-execute-src-block)))

;;;###autoload
(define-minor-mode orgstrap-edit-mode
  "Minor mode for editing with orgstrapped files."
  nil "" nil

  (unless (eq major-mode 'org-mode)
    (setq orgstrap-edit-mode nil)
    (user-error "`orgstrap-edit-mode' only works with org-mode buffers"))

  (cond (orgstrap-edit-mode
         (add-hook 'before-save-hook #'orgstrap--update-on-change nil t))
        (t
         (remove-hook 'before-save-hook #'orgstrap--update-on-change t))))
#+end_src
# orgstrap-embed-normalization-code
# is a potential future variable but for sanity
# I am leaving it out for now because it is easier
# to have a rule that says "always use orgstrap-embedded-norm-func"
# and then we don't have to wonder about it, the size tradeoff can
# be made by the user based on their use case
*** Init
A note on filter aka =cl-remove-if-not= in =orgstrap--add-file-local-variables= at [[(clrin)]].
| emacs version | require |
|---------------+---------|
| < 24          | 'cl     |
| < 25          | 'cl-lib |
| < 27          | 'seq    |
The most portable thing to do for now is =(require 'cl-lib)= since we
don't currently support anything below 23. Then use =cl-remove-if-not=.

There is a similar issue with =pcase=, which is that in =emacs-24= the
syntax was closer to =cl-case= when dealing with symbols. Since =cl-lib=
is already in use, =cl-case= is the logical solution for portability.

#+caption: init helpers
#+name: orgstrap-init-helper-defuns
#+begin_src emacs-lisp :results none :lexical yes :noweb yes
;;; init helpers
(defvar orgstrap-link-message "jump to the orgstrap block for this file"
  "Default message for file internal links.")

(defvar orgstrap--local-variables nil
  "Variable to capture local variables from `hack-local-variables'.")

;; local variable generation functions

(defun orgstrap--get-min-org-version (info minimal)
  "Get minimum org mode version needed by the orgstrap block for this file.
INFO is the source block info.  MINIMAL sets whether to use minimal local vars."
  (if minimal
      (let ((coderef (nth 6 info))
            (noweb (org-babel-noweb-p (nth 2 info) :eval)))
        (if noweb
            "9.3.8"
          (let* ((body (nth 1 info))
                 (crrx (org-src-coderef-regexp coderef))
                 (pos (string-match crrx body))
                 (commented
                  (and pos (string-match
                            (concat (rx ";" (zero-or-more whitespace)) crrx) body))))
            ;; FIXME the right way to do this is similar to what is done in
            ;; `org-export-resolve-coderef' but for now we know we are in elisp
            (if (or (not pos) commented)
                "8.2.10"
              "9.3.8"))))
    "8.2.10"))

(defun orgstrap--have-min-org-version (info minimal)
  "See if current version of org meets minimum requirements for orgstrap block.
INFO is the source block info.
MINIMAL is passed to `orgstrap--get-min-org-version'."
  (let ((actual (org-version))
        (need (orgstrap--get-min-org-version info minimal)))
    (or (not need)
        (string< need actual)
        (string= need actual))))

(defun orgstrap--dedoc (sexp)
  "Remove docstrings from SEXP."
  ;; defun 3 defmacro 3 defvar 3
  (if (symbolp (elt sexp 0))
      (if (and (memq (elt sexp 0) '(defun defmacro defvar))
               (stringp (elt sexp 3))
               (or (eq (elt sexp 0) 'defvar)
                   (elt sexp 4)))
          (append (cl-subseq sexp 0 3) (cl-subseq sexp 4))
        sexp)
    (mapcar #'orgstrap--dedoc sexp)))

(defun orgstrap--local-variables--check-version (info &optional minimal)
  "Return the version check local variables given INFO and MINIMAL."
  `(
    (setq-local orgstrap-min-org-version ,(orgstrap--get-min-org-version info minimal))
    <<orgstrap-check-org-version>>))

(defun orgstrap--local-variables--norm (&optional norm-func-name)
  "Return the normalization function for local variables given NORM-FUNC-NAME."
  (let ((norm-func-name (or norm-func-name orgstrap-norm-func)))
    (cl-case norm-func-name
      (orgstrap-norm-func--prp-1.0
       '(
         <<block-orgstrap-norm-func--prp-1.0>>))
      (otherwise (error "Don't know that normalization function %s" norm-func-name)))))

(defun orgstrap--local-variables--norm-common ()
  "Return the common normalization functions for local variables."
  '(
    <<orgstrap-normalization-common-embed>>))

(defun orgstrap--local-variables--eval (info &optional minimal)
  "Return the portable or MINIMAL eval local variables given INFO."
  (let* ((minimal (or minimal orgstrap-use-minimal-local-variables))
         (minimal (and minimal (orgstrap--have-min-org-version info minimal))))
    (if minimal
        '(
          <<orgstrap-minimal-confirm-eval>>)
      '(
        <<orgstrap-expand-body>>

        <<orgstrap-portable-confirm-eval>>))))

(defun orgstrap--local-variables--eval-common ()
  "Return the common eval check functions for local variables."
  `( ; quasiquote to fill in `orgstrap-orgstrap-block-name'
    <<orgstrap-file-local-variables-common>>))

;; init utility functions

(defun orgstrap--new-heading-elisp-block (heading block-name &optional header-args noexport)
  "Create a new elisp block named BLOCK-NAME in a new heading titled HEADING.
The heading is inserted at the top of the current file.
HEADER-ARGS is an alist of symbols that are converted to strings.
If NOEXPORT is non-nil then the :noexport: tag is added to the heading."
  (save-excursion
    (goto-char (point-min))
    (outline-next-heading)  ;; alternately outline-next-heading
    (org-meta-return)
    (insert (format "%s%s\n" heading (if noexport " :noexport:" "")))
    ;;(org-edit-headline heading)
    ;;(when noexport (org-set-tags "noexport"))
    (move-end-of-line 1)
    (insert "\n#+name: " block-name "\n")
    (insert "#+begin_src elisp")
    (mapc (lambda (header-arg-value)
            (insert " :" (symbol-name (car header-arg-value))
                    " " (symbol-name (cdr header-arg-value))))
          header-args)
    (insert "\n#+end_src\n")))

(defun orgstrap--trap-hack-locals (command &rest args)
  "Advice for `hack-local-variables-filter' to do nothing except the following.
Set `orgstrap--local-variables' to the reversed list of read variables which
are the first argument in the lambda list ARGS.
COMMAND is unused since we don't actually want to hack the local variables,
just get their current values."
  (ignore command)
  (setq-local orgstrap--local-variables (reverse (car args)))
  nil)

(defun orgstrap--read-current-local-variables ()
  "Return the local variables for the current file without applying them."
  (interactive)
  ;; orgstrap--local-variables is a temporary local variable that is used to
  ;; capture the input to `hack-local-variables-filter' it is unset at the end
  ;; of this function so that it cannot accidentally be used when it might be stale
  (setq-local orgstrap--local-variables nil)
  (let ((enable-local-variables t))
    (advice-add #'hack-local-variables-filter :around #'orgstrap--trap-hack-locals)
    (unwind-protect
        (hack-local-variables nil)
      (advice-remove #'hack-local-variables-filter #'orgstrap--trap-hack-locals))
    (let ((local-variables orgstrap--local-variables))
      (makunbound 'orgstrap--local-variables)
      local-variables)))

(defun orgstrap--add-link-to-orgstrap-block (&optional link-message)
  "Add an `org-mode' link pointing to the orgstrap block for the current file.
The link is placed in comment on the second line of the file.  LINK-MESSAGE
can be used to override the default value set via `orgstrap-link-message'"
  (interactive)  ; TODO prompt for message with C-u ?
  (goto-char (point-min))
  (next-logical-line)  ; use logical-line to avoid issues with visual line mode
  (let ((link-message (or link-message orgstrap-link-message)))
    (unless (save-excursion (re-search-forward
                             (format "^# \\[\\[%s\\]\\[.+\\]\\]$"
                                     orgstrap-orgstrap-block-name)
                             nil t))
      (insert (format "# [[%s][%s]]\n"
                      orgstrap-orgstrap-block-name
                      (or link-message orgstrap-link-message))))))

(defun orgstrap--add-orgstrap-block ()
  "Add a new elisp source block with #+name: orgstrap to the current buffer.
If a block with that name already exists raise an error."
  (interactive)
  (let ((all-block-names (org-babel-src-block-names)))
    (if (member orgstrap-orgstrap-block-name all-block-names)
        (message "orgstrap block already exists not adding!")
      (orgstrap--new-heading-elisp-block "Bootstrap"
                                         orgstrap-orgstrap-block-name
                                         '((results . none)
                                           (lexical . yes))
                                         t)
      (orgstrap--with-block orgstrap-orgstrap-block-name
        (ignore params body-unexpanded body)
        ;;(error "TODO insert some minimal message or something")
        nil))))

(defun orgstrap--add-file-local-variables (&optional minimal norm-func-name)
  "Add the file local variables needed to make orgstrap work.
MINIMAL is used to control whether the portable or minimal block is used.
If MINIMAL is set but the orgstrap block uses features like noweb and
uncommented coderefs and function `org-version' is too old, then the portable
block will be used.  NORM-FUNC-NAME is an optional argument that can be provided
to determine which normalization function is used independent of the current
buffer or global setting for `orgstrap-norm-func'."
  ;; switching comments probably wont work ? we can try
  ;; Use a prefix argument (i.e. C-u) to add file local variables comments instead of in a :noexport:
  (interactive)
  (let ((info (save-excursion
                (org-babel-goto-named-src-block orgstrap-orgstrap-block-name)
                (org-babel-get-src-block-info)))
        (elv (orgstrap--read-current-local-variables)))
    (let ((lv-cver (orgstrap--local-variables--check-version
                    info
                    minimal))
          (lv-norm (orgstrap--local-variables--norm
                    norm-func-name))
          (lv-ncom (orgstrap--local-variables--norm-common))
          (lv-eval (orgstrap--local-variables--eval
                    info
                    minimal))
          (lv-ecom (orgstrap--local-variables--eval-common)))
      (let ((lv-commands (orgstrap--dedoc (append lv-cver lv-norm lv-ncom lv-eval lv-ecom)))
            (commands-existing (mapcar #'cdr (cl-remove-if-not (lambda (l) (eq (car l) 'eval)) elv)))) ;(ref:clrin)
        (cond ((equal commands-existing lv-commands) nil)
              ((not commands-existing)
               (let ((print-escape-newlines t))  ; needed to preserve the escaped newlines
                 ' ; one sexp per eval line, too hard to manage switching it out
                 (mapcar (lambda (sexp) (add-file-local-variable 'eval sexp)) lv-commands)
                 ;; easier to put it all in a single progn that we can id and swap
                 ;; yes it is harder to read, but that is why we have all the docs
                 ;; FIXME detect if orgstrap-global-mode is enabled and do nothing
                 (add-file-local-variable 'eval (cons 'progn lv-commands))))
              ;; we could try to do something fancy here, but it is much simpler
              ;; to just alert the user and have them fix it than trying to guess
              (t (error "Existing eval commands that do not match the commands to be installed have been detected.  Please remove those commands and run `orgsrap-add-file-local-variables' again or manually add the orgstrap file local variables.  The existing commands are as follows.\n%s" commands-existing)))))))

;; init user facing functions
;;;###autoload
(defun orgstrap-init (&optional prefix-argument)
  "Initialize orgstrap in a buffer and enable command `orgstrap-edit-mode'.
PREFIX-ARGUMENT is essentially minimal from other functions, when non-nil
the minimal local variables will be used if possible."
  (interactive "P")
  (unless (eq major-mode 'org-mode)
    (error "Cannot orgstrap, buffer not in `org-mode' it is in %s!" major-mode))
  ;; TODO option for no link?
  ;; TODO option for local variables in comments vs noexport
  (save-excursion
    (orgstrap--add-orgstrap-block)
    (orgstrap-add-block-checksum)
    (orgstrap--add-link-to-orgstrap-block)
    ;; FIXME sometimes local variables don't populate due to an out of range error
    (orgstrap--add-file-local-variables (or prefix-argument orgstrap-use-minimal-local-variables))
    (orgstrap-edit-mode)))
#+end_src

# (orgstrap--dedoc '(defvar lol 'hello))
# (orgstrap--dedoc '(defvar lol 'hello "there"))
# (orgstrap--dedoc '(defun lol () "there" 1))
# (orgstrap--dedoc '(defun lol (arg) "there" arg))
# (orgstrap--dedoc '(defun lol (arg) "there"))
# (orgstrap--dedoc '(defmacro lol (arg) "there"))
# (orgstrap--dedoc '(defmacro lol (arg) "there" arg))
# (orgstrap--dedoc '((defmacro lol (arg) "there" arg) (defvar lol 'hello "there")))
*** Extras
#+caption: extra helpers
#+name: orgstrap-extra-helper-defuns
#+begin_src elisp :noweb yes
;;; extra helpers
(defun orgstrap-update-src-block (name content)
  "Set the content of source block named NAME to string CONTENT.
XXX NOTE THAT THIS CANNOT BE USED WITH #+BEGIN_EXAMPLE BLOCKS."
  ;; FIXME this seems to fail if the existing block is empty?
  ;; or at least adding file local variables fails?
  (let ((block (org-babel-find-named-block name)))
    (if block
        (save-excursion
          (org-babel-goto-named-src-block name)
          (org-babel-update-block-body content))
      (error "No block with name %s" name))))

(defun orgstrap-get-src-block-checksum (&optional cypher)
  "Calculate of the checksum of the current source block using CYPHER."
  (interactive)
  (let* ((info (org-babel-get-src-block-info))
         (params (nth 2 info))
         (body-unexpanded (nth 1 info))
         (body (orgstrap--expand-body info))
         (body-normalized
          (orgstrap-norm body))
         (cypher (or cypher (orgstrap--current-buffer-cypher))))
    (ignore params body-unexpanded)
    (secure-hash cypher body-normalized)))

(defun orgstrap-get-named-src-block-checksum (name &optional cypher)
  "Calculate the checksum of the first sourc block named NAME using CYPHER."
  (interactive)
  (orgstrap--with-block name
    (ignore params body-unexpanded)
    (let ((cypher (or cypher (orgstrap--current-buffer-cypher)))
          (body-normalized
           (orgstrap-norm body)))
      (secure-hash cypher body-normalized))))

(defun orgstrap-run-additional-blocks (&rest name-checksum) ;(ref:oab)
  "Securely run additional blocks in languages other than elisp.
Do this by providing the name of the block and the checksum to be embedded
in the orgstrap block as NAME-CHECKSUM pairs."
  (ignore name-checksum)
  (error "TODO"))
#+end_src

Ideally we want to call [[(oab)][orgstrap-run-additional-blocks]] as
=(orgstrap-run-additional-blocks "additional-block-name" "checksum-value-hash-thing" "ab2" "cs2")=
It probably makes sense to house this in its own orgstrap-aux block or something.
I want to keep the file local variables as minimal as possible, so having another
aux block that could be automatically updated with the names and hashes of additional
blocks would be nice ... probably via something like =orgstrap-add-additional-block=
but it will not go in the local variables because we want there to be some hope of
orgstrap being portable to other platforms outside of Emacs at some point in the
very distant future, so keeping the machinery outside of the org file itself as
minimal as possible is critical.
** orgstrap.el :noexport:
# XXX TODO it would be a super cool feature if xref could resolve to elisp source
# blocks in org-mode files, because then half the need for the .el file would go away
#+caption: Retangle this if something changes.
#+name: orgstrap.el
#+header: :exports none
#+begin_src elisp :noweb yes :eval never :tangle ./orgstrap.el
;;; orgstrap.el --- Bootstrap an Org file using file local variables -*- lexical-binding: t -*-

;; Author: Tom Gillespie
;; URL: https://github.com/tgbugs/orgstrap
;; Keywords: lisp org org-mode bootstrap
;; Version: 1.0
;; Package-Requires: ((emacs "24.4"))

;;;; License and Commentary

;; License:
;; SPDX-License-Identifier: GPL-3.0-or-later

;;; Commentary:

;; orgstrap is a specification and tooling for bootstrapping Org files.

;; It allows Org files to describe their own requirements, and
;; define their own functionality, making them self-contained,
;; standalone computational artifacts, dependent only on Emacs,
;; or other implementations of the Org-babel protocol in the future.

;; orgstrap.el is an elisp implementation of the orgstrap conventions.
;; It defines a regional minor mode for `org-mode' that runs orgstrap
;; blocks.  It also provides `orgstrap-init' and `orgstrap-edit-mode'
;; to simplify authoring of orgstrapped files.  For more details see
;; README.org which is also the literate source for this orgstrap.el
;; file in the git repo at
;; https://github.com/tgbugs/orgstrap/blob/master/README.org
;; or whever you can find git:c1b28526ef9931654b72dff559da2205feb87f75

;; Code in an orgstrap block is usually meant to be executed directly by its
;; containing Org file.  However, if the code is something that will be reused
;; over time outside the defining Org file, then it may be better to tangle and
;; load the file so that it is easier to debug/xref functions.  The code in
;; this orgstrap.el file in particular is tangled for inclusion in one of the
;; *elpas so as to protect the orgstrap namespace and to make it eaiser to
;; use orgstrap in Emacs.

;; The license for the orgstrap.el code reflects the fact that the
;; code for expanding and hashing blocks reuses code from ob-core.el,
;; which at the time of writing is licensed as part of Emacs.

;;; Code:

(require 'org)

<<orgstrap-run-helper-defuns>>

<<orgstrap-edit-helper-defuns>>

<<orgstrap-init-helper-defuns>>

<<orgstrap-extra-helper-defuns>>

(provide 'orgstrap)

;;; orgstrap.el ends here

#+end_src
# have to have an empty line at the end so that a newline shows up
# when tangled ... surely this is a bug?
** Testing :noexport:
#+name: test-portable
#+begin_src bash :var THIS_FILE=(buffer-file-name) :results none
emacs-24 -q $THIS_FILE
emacs-25 -q $THIS_FILE
emacs-26 -q $THIS_FILE
emacs-27 -q $THIS_FILE
emacs-28-vcs -q $THIS_FILE
#+end_src

#+name: test-minimal
#+begin_src bash :var THIS_FILE=(buffer-file-name) :results none
emacs-24 -q orgstrap-minimal.org
emacs-25 -q orgstrap-minimal.org
emacs-26 -q orgstrap-minimal.org
emacs-27 -q orgstrap-minimal.org
emacs-28-vcs -q orgstrap-minimal.org
#+end_src
* Change log
:PROPERTIES:
:CUSTOM_ID: change-log
:END:
** 1.1
- Renamed existing =orgstrap-mode= to =orgstrap-edit-mode=.

  This is a BREAKING CHANGE. Better make the change sooner rather than later.

- Added the new =orgstrap-mode= implementation.

  This is a regional minor mode for =org-mode= which makes it possible to use orgstrap
  without the embedded local variables. This allows for greater security at the expense of
  portability, depending on the exact use case. By a stroke of good fortune it is possible
  to use the =hack-local-variables= hooks to trap and remove the embedded local variables
  if they are present so that the orgstrap block is not evaluated twice.

- Added =orgstrap-always-edit= as a custom variable.

  If non-nil then =orgstrap-edit-mode= will be automatically activated by =orgstrap-mode=.
* Best practices
** Use the system package manager.
There is a big difference between using a script to install a program directly
from the internet and using a script to ask the host system to install a program.

Even if you audit a random script from the internet it is unlikely that you will
be able to do due diligence. On the other hand, if you ask your system package
manager to install something for you, there is a much better chance that it has
at least been somewhat audited, and there is usually an existing process for
getting a package into the system which helps to mitigate certain types of attacks.

To give a military example it is the difference between inspecting and accepting a
package from a random person because they say you asked for it yesterday (maybe you
did!) versus only every allowing packages to come through procurement. You are much
less likely to get a bomb or a packaged rigged to exfil data if you go through
procurement because there is an established process for how to do things and that
process enshrines generations experience about how to not get blown up by the pizza guy.

So, if you are writing instructions that require a certain tool, it is better to tell
whoever is following them to ask procurement to get the tool for them than to tell them
to going out to the hardware store and get it themselves, or worse, give them the address
of a random tool delivery man who happens to be a good buddy of yours. Even if everyone
involved is trustworthy those kinds of relationships are much easier for some third party
to compromise and use for their own purposes.

The obvious corollary when you are the user rather than the author, is that if you
encounter instructions that ask you to directly install software from a random place
you should be suspicious, even, perhaps especially, if that random place is housed
within a larger reputable site. If you're not in a hurry, ask for the software to be
packaged, or package it yourself so that it can go through the process.
* Bootstrapping to Emacs, bootstrapping to Org.
:PROPERTIES:
:CUSTOM_ID: bootstrapping-to-emacs-bootstrapping-to-org
:END:
If you are dealing with a system that does not have a working
version of Emacs then all these fancy =orgstrap= tricks don't
do us very much good.

The following section tangles a nearly unreadable [[file:get-emacs.el.sh.ps1][file]]
that can be used to bootstrap Emacs onto on a variety of systems.
The blocks below break it up into readable chunks.

# TODO post the checksums for it when ready

I'm not sure that anyone should use it, but it seems easier than
other approaches I have taken for trying to get Emacs installed
on a non-technical or semi-technical user's system. The variety
of different ways that one can obtain, install, configure, and
customize Emacs makes it particularly bewildering.

=doom-emacs= =bin/doom= provided the initial inspiration for this
solution[fn::Not that anyone should ever want to be inspired to
create such a monstrosity.  But hey, if software packaging and
distribution were a solve problem we wouldn't be doing this right
now and there would be no such thing as cybercrime and we would
all live in a wild happy techno-utopian future ...]. The inspiration
for the problem was entirely of my own making and comes from a painful
experience trying write instructions that were followable[fn::
[[https://github.com/SciCrunch/sparc-curation/blob/master/docs/setup.org#setup]]]
for how to run a block in an Org file from scratch, thinking that if
they could just manage to do that then _hard_ part of the setup would
be handled for them by the code in the Org mode file. Live and learn.

So here we are, not quite at curl pipe bash, but hopefully at a form
that is slightly more auditable[fn::Which means that this section may
need to be broken out into its own file and published separately.].
** Posix bootstrap
# =apt= has some insane name collision on gentoo from the eselect-java ebuild.
Package manager commands.
#+name: &var-cmds
: emerge apt yum dnf pacaman nix-env guix brew

We start with builtin package managers rather than parasitic package managers
because anyone using one of those should know enough to either already have emacs
installed or to figure out how to get it installed without this script.

# shells ... bash dash zsh fish powershell
We don't use hyphen-minus =-= in function names so that we can support bash running in posix mode.

You would think that it would be easier to securely retrieve a file from
the internet and check that its checksum matches, but just handling the
cases for existing files and checksum failures is enough to increase the
size and we haven't even dealt with supporting different cyphers yet! Some
deduplication could be achieved by creating another function or two.
#+name: posix-bootstrap
#+begin_src sh :noweb yes
echo "Bootstrapping in posix mode."
scurl () {
    # safe(r) curl, yet still scurrilous thus scurl
    # example: scurl my-audited-checksum https://example.org/file.ext /tmp/file.ext
    local CHECKSUM="${1}"
    local URL="${2}"
    local path="${3}"
    local dname="$(dirname "${path}")"
    local fname="$(basename "${path}")"
    if [ -f "${path}" ]; then
        echo "$(sha256sum "${path}" 2>/dev/null || shasum -a 256 "${path}")" | \
        awk '$1!="'"${CHECKSUM}"'" { exit 1 }'
        CODE=$?
        if [ $CODE -ne 0 ]; then
            echo failed with $CODE
            echo "${path}" existing checksum does not match new checksum.
            local efail_path="$(mktemp -p "${dname}" "${fname}"._existing_failure_.XXXXXX)"
            mv "${path}" "${efail_path}"
        else
            return 0
        fi
    fi
    local temp_path="$(mktemp -p "${dname}" "${fname}"._fetching_.XXXXXX)"
    #cmd="$(command -v sha256sum 2>/dev/null || (shasum -a 256))"
    # too many ways a streaming check can go wrong that we can't handle correclty
    # set -o pipefail is not portable
    #curl --location "${URL}" | tee "${temp_path}" | ${cmd} || return $?
    curl --location "${URL}" --output "${temp_path}" || return $?
    echo "$(sha256sum "${temp_path}" 2>/dev/null || shasum -a 256 "${temp_path}")" | \
    awk '$1!="'"${CHECKSUM}"'" { exit 1 }'
    CODE=$?
    if [ $CODE -ne 0 ]; then
        echo failed with $CODE
        echo "${temp_path}" checksum did not pass! something evil is going on!
        local fail_path="$(mktemp -p "${dname}" "${fname}"._checksum_failure_.XXXXXX)"
        mv "${temp_path}" "${fail_path}"
    else
        mv "${temp_path}" "${path}"
    fi
}
function package_manager {
    local full;
    cmds=(<<&var-cmds()>>)
    for cmd in "${cmds[@]}"; do
        full=$(command -v ${cmd} 2>&1) && break;
    done
    echo "${cmd}"
}
function posix_bootstrap {
    cmd=$(package_manager)
    case $cmd in
        emerge)  $cmd app-editors/emacs ;;
        yum)     $cmd -y install emacs-nox ;;
        dnf)     $cmd -y install emacs-nox ;;
        pacman)  $cmd --noconfirm -S emacs-nox ;;
        apt)     $cmd -y install emacs-nox ;;
        nix-env) $cmd -i emacs-nox ;;
        guix)    $cmd install emacs-nox ;;
        brew)    $cmd cask install emacs ;;
        ,*)       echo No package manager found! Checked <<&var-cmds()>>. ;return 1 ;;
    esac
    return $?
    # TODO
    # get-yourself-a-real-package-manager
    # none -> os detection -> get the right one -> run this again
    echo yay posix
    # should probably run emacs in here ??
}
function missing_emacs {
    echo "<<&message-emacs-missing()>>"
    local BCMD="$(typeset -f package_manager); $(typeset -f posix_bootstrap); posix_bootstrap"
    if command -v sudo; then
        sudo "$0" -c "${BCMD}" || exit $?
    else
        echo For su on ${HOSTNAME} 1>& 2;
        su -c "${BCMD}" || exit $?
    fi
}
# FIXME sort out the argument passing
( echo "${EMACS}" | grep -q "term" ) && EMACS=emacs || EMACS=${EMACS:-emacs}
command -v $EMACS >/dev/null || missing_emacs &&
$EMACS --no-site-file --script "$0" -- "$@"
CODE=$?
exit $CODE
#+end_src

#+name: &message-emacs-missing
: Emacs missing, preparing to bootstrap.

FIXME I do NOT like the fact that choco is effectively pulling a =curl | bash= here.
This needs a signature.
# https://chocolatey.org/install#install-with-powershellexe
#+name: powershell-bootstrap
#+begin_src powershell :noweb yes
Write-Host "Bootstrapping in windows mode."
$EMACS = if ($Env:EMACS -eq $null) { "emacs" } else { $Env:EMACS }
function bootstrap-windows {
    if (-not (Get-Command $EMACS -errorAction SilentlyContinue)) {
        Write-Host "<<&message-emacs-missing()>>"
        if (-not (Get-Command choco -errorAction SilentlyContinue)) {
            Write-Host "Chocolatey missing, preparing to bootstrap."
            Write-Host "Install chocolatey? [y/N]"
            if ('y', 'Y' -contains $Host.UI.RawUI.ReadKey("NoEcho,IncludeKeyDown").Character) {
                Set-ExecutionPolicy AllSigned -Scope Process -Force;
                [System.Net.ServicePointManager]::SecurityProtocol = [System.Net.ServicePointManager]::SecurityProtocol -bor 3072;
                Invoke-Expression ((New-Object System.Net.WebClient).DownloadString('https://chocolatey.org/install.ps1'))
                } else { Write-Host "Not installing chocolatey. If you want to continue you can install emacs manually.";
                    throw "failed" } }
        choco install $EMACS } }
# FIXME may also need to use $MyInvocation.MyCommand.Path .Name
try {
    bootstrap-windows
    emacs --script $MyInvocation.MyCommand.Source -- $args
} finally {
    exit
}
#+end_src
** Posix powershell switch
Right now the switch only works with bash, zsh, and powershell because
https://unix.stackexchange.com/a/71137 is more than I want
to try to deal with here. It will also run under =sh= if it
points to bash running in =--posix= mode.

It is unfortunate that there is no way to get this to work with dash,
otherwise we could set =sh= for the shebang, but we can't due to the
incompatible function syntax.

The approach comes from https://stackoverflow.com/a/39422067. The block below
is syntactically valid bash, zsh, and powershell. In a posix shell the quoted
commands and ="exit"= run, whereas in powershell they are skipped. We then engage
in some unspeakable hackery and parse the input file and grep for =#px= or =#ps=
to select the code for the specific platform (see ref:bootstrap-from-shell).
We then eval it[fn::If you are still nodding along in agreement, know that
I am running in terror from this code block even as I write this footnote.].
# A slight redeeming factor is that the use of =$0= means that you can't
# curl pipe bash the output script because there is no =$0=. On the other hand
# if you do want to simplify life by using streaming insecurities then we will
# need to come up with an alternate approach.

#+caption: This is where the magic happens.
#+name: posix-powershell-switch
#+begin_src bash :noweb yes
function posix () {
  eval "$(grep '^":";\ #px' $0 | sed -e 's/^":";\ #px //')"
  return "$?"
}
function quirk () {
  test "$ZSH_VERSION" && set +nofunctionargzero
}
function unquirk () {
  test "$ZSH_VERSION" && set nofunctionargzero
}
"quirk"
"posix"
"unquirk"
"exit"  # NOTE can't pass error code out easily here
((Get-Content $MyInvocation.MyCommand.Source) -match '^":";\ #ps ' -replace '^":";\ #ps ') -join "`n" | Invoke-Expression
exit
#+end_src

Many variants of the test for quirk have been tried and failed.
This variant works but was not selected because =test= is more
portable (see https://stackoverflow.com/a/6535252).
#+begin_src bash
function quirk () {
  (( $ZSH_VERSION )) && set +nofunctionargzero
}
#+end_src
Furthermore, =test= is compatible with powershell syntax so it is better to use that
for bootstrapping. On all my windows systems =test= is available, but I suspect
that it is coming from something I have installed and cannot be relied upon.

Quirks are required for zsh due to the fact that it will set =$0= to the name
of the function which breaks grepping =$0= since we encounter the name of the
function not the name of the source file.
*** Tests                                                          :noexport:
Test blocks to ensure shell portability.
#+begin_src powershell :noweb yes :results output :exports none
<<posix-powershell-switch>>
#+end_src

#+begin_src zsh :noweb yes :results output :exports none
<<posix-powershell-switch>>
#+end_src
** Emacs lisp
This block is somewhat more straight forward. All this is really just an
Emacs lisp file! ... but with some quoted colons and comments.
It just so happens that those quoted colons and comments are no-ops
in both powershell and bash/zsh so we can include the shell functionality
needed safely hidden away from elisp behind the comments.

#+name: bootstrap-from-shell
#+header: :shebang "#!/usr/bin/env bash"
#+begin_src emacs-lisp :noweb no-export :tangle ./get-emacs.el.sh.ps1
":"; # -*-emacs-lisp-*-
":"; <<posix-powershell-switch>>
":"; #px <<posix-bootstrap>>
":"; #ps <<powershell-bootstrap>>
(message "We have Emacs.")
#+end_src

At this point the basics of the script should be working on
darwin, various linux distros, various bsds, and windows. At the moment
only windows and a subset of linux distros can bootstrap from scratch.
There is also a question of whether forcing a package manager on an
unsuspecting user is a good idea. I tend to say yes, but maybe we need
to make sure that they are a little less unsuspecting? Though there is
nothing like a cold baptism into the world of proper systems administration.
*** Tests                                                          :noexport:
Bash
#+name: bootstrap-from-shell-bash
#+begin_src bash :noweb no-export :results output :exports none
<<bootstrap-from-shell>>
#+end_src

Bash in posix mode.
If you try to run this block via org-babel on a Debian derived
distro this will fail, see the =:shebang= header for details.
#+name: bootstrap-from-shell-sh
#+header: :shebang "#!/usr/bin/env -S bash --norc --posix"
#+begin_src sh :noweb no-export :results output :exports none
<<bootstrap-from-shell>>
#+end_src

Zsh
#+name: bootstrap-from-shell-zsh
#+begin_src zsh :noweb no-export :results output :exports none
<<bootstrap-from-shell>>
#+end_src

Powershell
#+name: bootstrap-from-shell-powershell
#+begin_src powershell :noweb no-export :results output :exports none
<<bootstrap-from-shell>>
#+end_src
* Examples
** Installing a package
With orgstrap in melpa I'm going to rule that, while a fun idea, the though
of using the orgstrap block for this file to stick the machinery in a users
init.el somehow is not the best approach (to say the least) to providing the
functionality contained in this file. The best approach is to include the
following in your orgstrap block so that it is clear what the user is in for.
I'm not entirely sure how to make it possible to make handling optional
dependencies possible ... probably using a =:var= header option that doesn't
get hashed?

#+name: install-orgstrap
#+begin_src elisp
;; install `orgstrap'
(add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/") t)
(if (fboundp #'use-package)
    (use-package orgstrap)
  (package-install 'orgstrap))
;; TODO detect the use of quelpa/straight/borg/etc.
#+end_src
* Background, file local variables, and checksums
As mentioned above, the primary use case for =orgstrap= was that I was sick of having
to work around the limitation that I had to do one of four things. I either one, had
to remember to eval the source block containing defuns used later before I could
eval other source blocks that used those functions in headers, or two, had to put those
functions in =init.el=, destroying the ability to use org files as standalone self describing
portable and reusable computational artifacts, three, had to copy and paste verbose
elisp bits around to achieve what I wanted, or four, had to double tangle a file so that
the results of the first tangle could be loaded before calling the second tangle so that
the functionality would be available (this also produces the situation described in three).
Furthermore, it is hard for humans to follow all the steps needed to get everything
working -- even when 'everything' is just invoking =C-c C-c= on a single source block
I still forget. This can lead to _bad things_ if some of those source blocks were
interdependent, or proceeded with a nil, etc.

File local variables to the rescue!
I'm slightly embarrassed to say how long it took me to arrive at the current solution.
I had known for quite a while that file local variables are a pathway to +abilities that+
the evils of arbitrary code execution, but it didn't click that all I was looking for was
the ability to just run some arbitrary elisp code every time a particular file was loaded,
which of course is exactly what file local variables are for.

The only question then was how to avoid the very real dangers of enabling arbitrary code
execution of plain text. Actually it was more along the lines of "How can I keep org-babel
happy without also pwning myself?" Fortunately =org-confirm-babel-evaluate= can be customized
to be a function that accepts the body of the code to be evaluated. Therefore we can do the
following.

When creating a file.
1. *Hash the block to be run before distributing the file.*
   Make sure to test if there are any changes to the header.
   For example I have a bad habit of accidentally setting
   =:noweb no-export= incorrectly without the dash and that will
   prevent the checksum from updating if a nowebbed block changes.
2. *Embed the checksum in the file local variable property line.*
   The property line is highly visible as the first line of the
   file. This makes it easy for users to verify that the embedded
   checksum matches a known independent checksum (running step 2).
   Thus if the embedded checksum does not match a known checksum
   the user will notice, and if the code to be executed does not
   match the embedded checksum then the user will at least be
   prompted by org-mode to run the block even in the case where
   they accepted the file local variables. Emacs also prompts for
   verification of the property line value which is another
   opportunity for the user to check.
3. *Publish the checksum independent of the file itself.*
   It is trivial for someone to change the contents of the orgstrap block
   and rerun =M-x= =orgstrap-add-block-checksum=. Therefore known checksums
   need to be published independent of the files themselves.

When running a file.
1. *Audit, accept, and store permanently the eval file local variables.*
   Storing audited variables permanently is critical for improving signal to noise
   so that unexpected mismatches retain their salience and can elicit the correct
   response (i.e., suspicion).
   # XXX there may be an issue here if the property line tags along with the rest
   # because we want to be able to mark the exact variables used in this file
   # as safe and if they are couple to a random hash that is bad
2. *Audit the orgstrap block*
   I assume most people are not going to do this. However, one of the advantages
   of the current approach is that the same orgstrap blocks can be reused across
   multiple files which reduces the audit load such that one only needs to review
   unique orgstrap blocks, not all files. [fn::NOTE there are certain patterns inside
   blocks that are NOT safe to accept because they introduce a level of indirection
   that orgstrap cannot verify. Examples of these kinds of dangerous blocks are ones
   that make any reference to other blocks in the file via some means other than noweb.
   This isn't really surprising, and for use cases where =org-babel-execute-src-block=
   is called multiple times on different blocks, the default execution protection will
   work. In addition, any blocks which want to run automatically without prompting should
   use the =orgstrap--confirm-eval= function (see [[file:::#future-work][Future work]]).]
3. *Verify that the embedded checksum matches the independent checksum.*
   A known embedded checksum matching the content checksum only means that the content
   matches the content observed by the provider of the independent checksum
   (assuming no hash collisions).
4. *Observe whether org-mode complains that the orgstrap block has changed.*
* Future work
:PROPERTIES:
:CUSTOM_ID: future-work
:END:
** Spec extension to support arbitrary orgstrap block names.
Since all the conventions for how this is done are defined locally by each file, you could
in principle rename the special block as you see fit, perhaps from =orgstrap= to =main= if
you need to pretend that the file is actually c source code with some special syntax.
However, this is not advisable if you care about portability since it depends on an
implementation detail of orgstrap.el which is not required by the specification. Namely
that =orgstrap-orgstrap-block-name= is not required as one of the prop line local
variables. Given the desire for the orgstrap machinery to be as unobtrusive as possible,
it is unlikely that support for an arbitrary name for the block will be added to the spec.

That said, it is worth considering how and whether to update the spec so that a conforming
implementation could do this if it wanted to. All the change does is move a convention to
an optional variable. Maybe a compact variable such as orgstrap-bn could be specified as
an optional prop line variable, and if absent the orgstrap block name defaults to
orgstrap, otherwise the block name searched is the value of the variable.

Still not 100% sure about this. It would increase the complexity of the implementation for
sure.  It will require updating how and when we populate the link to orgstrap block, and
makes auditing more difficult. It also opens up a way to trick the user, namely by having
a link to an innocent looking orgstrap block, and no convention set in the prop line, or
maybe even having it in the prop line (people are habitual and assume things are not
present when they are), and then using setq-local, or some other means to change the block
name to a malicious block elsewhere in the file. Implementations would have to know to
check for this and fail if it was detected. Basically we would have to specify that if a
block named orgstrap is present in a file when orgstrap-bn is present and not set to
orgstrap, then it is a fatal error and orgstrap will not continue. Sticking this in the
900 or so chars we have left for further features seems like it would be a stretch, but
might be possible.
** Security considerations
=orgstrap= currently does not check all the headers or vars properties that materialized
onto a source block we probably need to do this. For the time being users need to check
for any hidden header properties that might be attached if the source block is buried
within a tree somewhere.
** Batch mode
There are a number of use cases for being able to process orgstrapped files in batch mode.
For example being able to load a file and have it automatically tangle itself vastly simplifies
a number of different workflows. =emacs -q --batch -l orgstrap-known-safe.el my-file.org= seems
like a reasonable approach. Essentially =orgstrap-known-safe.el= needs to contain the safe eval
blocks and the audited hashes so that local variable prompts will not be triggered since they
always return no when in batch mode. One additional feature is be to able to pass the checksum
on the command line. The eval variables would still have to be loaded in some way, but avoiding
the need to open and edit =orgstrap-known-safe.el= for each new file, and possibly edit it again
to remove the approval in the future.
** Run once
In principle the simplest way to do this is to use the =:cache yes= header on a block.
However, unless the state is persisted into a users =init.el= file or equivalent, then
the file would need a way to know that it had not been run when opened again in a new
Emacs session. Similar issue with opening the same file in multiple Emacs sessions at
the same time. The block simply will not run again if the cached result is present.

Therefore, since =:cache yes= by itself is a dead end for ensuring that functionality
is always available any time a file is loaded there are a couple of options.
1. Persist to =init.el=. This is evil.
2. Request to tangle and install as package.
   A variant of this is simply to use packages.el to install
   the desired functionality in a persistent way in combination
   with accept klobbering.
3. Figure out how to transparently wrap an elisp block in =unless=.
4. Advise =defun= (say what!?)?  @@comment: TERROR@@
5. Figure out how to un-cache a block when Emacs exits.
   This will fail in nasty, unpredictable, and hard to debug ways.
6. Set =:cache (if (boundp 'orgstrap-already-run) "yes" "no")=.
   This ALMOST works. If =:cache no= embedded the sha1 sum then
   we would be golden. *This seems like the best bet.*
7. Accept klobbering.
8. Advise org-babel-eval to run with org-babel-sha1-sum even when cache is not set to yes
** Tangle once
When bootstrapping a new system there are many times when want to create a
file only if it does not already exist. The =:tangle= header does not support
this use case, but we can implement it anyway using the example below.
#+name: tangle-once-example
#+begin_src org
,#+name: orgstrap
,#+begin_src elisp
(defun tangle-once (path) (if (file-exists-p path) "no" path))
,#+end_src

,#+begin_src bash :tangle (tangle-once "./path-to-tangle")
echo lol
,#+end_src
# I think I've seen this before but you apparently can't have ,#+end_src on the line before #+end_src ... fun bug
#+end_src
** Multiple blocks
There must be only a single one of those blocks so that the rest of
the blocks can safely use the functions defined in the orgstrap block.

A single elisp block is sufficient to enable nearly all use cases involving
tangling source blocks to file without having to fight the prompts. However,
it is very much not sufficient for any use cases that involve other languages.
This is particularly an issue for org files that want to bootstrap whole systems.

The simplest solution to me seems to be to add a second prompt variable which is
an alist of source block checksums and names[fn::the names are not technically required
but are for human readability]. As soon as the =orgstrap= block is run
=orgstrap--confirm-eval= is no longer needed and can be replace with a function
that validates the other blocks from the prompt variable.

This seems like a tractable approach, but also over complicated because it is surely
easier in a case like this where blocks are very unlikely to be reused across org files
to simply =(setq-local org-confirm-babel-evaluate nil)= and tell people to audit the
whole file. The alternative in that case might be to hash all the source blocks and
validate all of them at once at the start of the orgstrap block. This might need some
additional machinery, not entirely sure, maybe just have =orgstrap-all-blocks-checksum=
that can be used in cases like that. The advantage here is that the core of the process
can be verified once and then the documentation around it can change and grow as needed.
** Remove defun docstrings from hashing
One additional source of noise in addition to comments are defun and
defmacro docstrings. These should be dropped from the tree if they are
present. This is now partially implemented via =orgstrap--dedoc=.
** Deterministic semantics preserving reordering
Reorder the expressions used in the orgstrap block alphabetically (or something like that)
according to a deterministic rule, but not in a way that changes program semantics.
For example a function definition cannot be moved after a top level invocation of that
function.
1. defuns with different names can be reordered
2. defuns with the same name can be reordered as a block but cannot
   internally be reordered because the order of shadowing matters
3. While it might be nice to completely erase the names of functions as well
   as internal variable names, this would make it trivial to shadow existing
   function names in ways that are malicious. The exact names matter, so we
   have to preserve them. Also the cost of not being able to tell that
   =(lambda (a) (+ a a))= and =(lambda (b) (+ b b))= are the same seems fairly
   small.
4. One potential approach is to lift all defuns to the top, and then function calls
   or whatever the more generic procedure invocation means. The simple local rule
   is that all definitions must occur before usage except in the case where there is
   a shadowing even that happens after a first invocation. This is annoying, but
   if a call to a function happens before that function is defined we have to assume
   that the call is calling some other function and those statements cannot be reordered.
   So the ordering is calls to functions with names matching any later defuns or
   any later assignment. Then defuns and assignments, finally procedure invocations
   which might also include assignments. I get the sense that this is covered under
   some part of compiler theory but can't quite put my finger on it.
** Smart update/upgrade using the version specifier
This is much easier now that I have put all the normalization and hashing
machinery in a single eval local variable, and it no longer needs to rely
on the version of the block, just the version of the normalization function,
which is currently embedded along with everything else.
** Figure out how to demo loading the packages used in this file
#+begin_src elisp
(use-package org-ref)
#+end_src
** TODO resolve the issue with tabs in < 26                        :noexport:
** TODO command to checksum the file local variables               :noexport:
** TODO ruby org so that github can render footnotes correctly     :noexport:
[[file:~/git/NOFORK/org-ruby]]
** DONE orgstrap-mode
If orgstrap is installed, then having a orgstrap-mode which could add itself to
org-mode-hook when enabled would supersede the local variables list implementation, or
possibly just do it when the local variables version was absent. Disabling the local
variables when orgstrap-mode is detect to be on might be possible, but without
it the user would have to explicitly decline the variables or risk having the orgstrap
block run twice. Going to have to get this resolved before doing any announcements since
it could leave users with quite a bit of pain if we don't get that check in before people
start using it.
** DONE Auto update block checksum on save
Before save hook and/or before commit hook to automatically update the block checksum.
** DONE use orgstrap to automatically keep example blocks in sync  :noexport:
** DONE melpa                                                      :noexport:
Woo! https://melpa.org/#/orgstrap
** DONE Determine whether to use minimal or portable based on the :noweb header
This is essentially what is implemented via =orgstrap--have-min-org-version=.
The choice to prefer the minimal local variables is left up to the user and
is controlled via the =orgstrap-use-minimal-local-variables= custom variable.
The only time when minimal is not used is if the version of org that is drafting
the file is too old (i.e. < =9.3.8=).
* Local Variables Footer :noexport:

# Local Variables:
# eval: (progn (setq-local orgstrap-min-org-version "8.2.10") (let ((actual (org-version)) (need orgstrap-min-org-version)) (or (fboundp #'orgstrap--confirm-eval) (not need) (string< need actual) (string= need actual) (error "Your Org is too old! %s < %s" actual need))) (defun orgstrap-norm-func--prp-1\.0 (body) (let ((print-quoted nil)) (prin1-to-string (read (concat "(progn\n" body "\n)"))))) (unless (boundp 'orgstrap-norm-func) (defvar orgstrap-norm-func orgstrap-norm-func-name)) (defun orgstrap-norm-embd (body) (funcall orgstrap-norm-func body)) (unless (fboundp #'orgstrap-norm) (defalias 'orgstrap-norm #'orgstrap-norm-embd)) (defun orgstrap-org-src-coderef-regexp (_fmt &optional label) (let ((fmt org-coderef-label-format)) (format "\\([:blank:]*\\(%s\\)[:blank:]*\\)$" (replace-regexp-in-string "%s" (if label (regexp-quote label) "\\([-a-zA-Z0-9_][-a-zA-Z0-9_ ]*\\)") (regexp-quote fmt) nil t)))) (unless (fboundp #'org-src-coderef-regexp) (defalias 'org-src-coderef-regexp #'orgstrap-org-src-coderef-regexp)) (defun orgstrap--expand-body (info) (let ((coderef (nth 6 info)) (expand (if (org-babel-noweb-p (nth 2 info) :eval) (org-babel-expand-noweb-references info) (nth 1 info)))) (if (not coderef) expand (replace-regexp-in-string (org-src-coderef-regexp coderef) "" expand nil nil 1)))) (defun orgstrap--confirm-eval-portable (lang body) (not (and (member lang '("elisp" "emacs-lisp")) (let* ((body (orgstrap--expand-body (org-babel-get-src-block-info))) (body-normalized (orgstrap-norm body)) (content-checksum (intern (secure-hash orgstrap-cypher body-normalized)))) (eq orgstrap-block-checksum content-checksum))))) (defalias 'orgstrap--confirm-eval #'orgstrap--confirm-eval-portable) (let ((ocbe org-confirm-babel-evaluate)) (setq-local orgstrap-norm-func orgstrap-norm-func-name) (setq-local org-confirm-babel-evaluate #'orgstrap--confirm-eval) (unwind-protect (save-excursion (org-babel-goto-named-src-block "orgstrap") (org-babel-execute-src-block)) (setq-local org-confirm-babel-evaluate ocbe))))
# End:
